plugins {
    id "java-library"
}
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    api 'com.google.code.gson:gson:2.10.1'
    api 'com.squareup.okhttp3:okhttp:4.12.0'
    api 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.1.13'
    testImplementation 'org.reflections:reflections:0.9.12'
}

jar {
    manifest {
        attributes "Automatic-Module-Name": "telegram.bot.api"
    }
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

javadoc {
    options.encoding = 'UTF-8'
}

test {
    if (project.hasProperty('excludeTests')) {
        exclude project.property('excludeTests')
    }
}

tasks.withType(Test).configureEach {
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}

tasks.register('prTest', Test) {
    // TODO move "functional" tests into specific dir and exclude it
    exclude '**/Base64Test.class'
    exclude '**/CheckTelegramAuthTest.class'
    exclude '**/PaymentsTest.class'
    exclude '**/TelegramBotTest.class'
    exclude '**/UpdatesListenerTest.class'
}

apply from: rootProject.file('gradle/gradle-publish-plugin.gradle')